program = { SOI ~ list* ~ EOI }

form = {
    list
  | quoted
  | string
  | number
  | nil
  | bool
  | symbol
}

list = {
    "(" ~ form* ~ ")"
}

quoted = {
    "'" ~ form
}

symbol = @{
    !ASCII_DIGIT ~ (ASCII_ALPHANUMERIC | ":" | "_" | "-" | "+" | "*" | "/" | "?" | "!" | "<" | ">" | "=" | ".")+
}

number = @{
    "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT+)?
}

string = ${
    "\"" ~ strinner ~ "\""
}

strinner = @{ str_char* }

str_char = _{
    "\\\""
  | "\\n"
  | "\\t"
  | "\\\\"
  | !"\"" ~ ANY
}

nil = @{ "nil" }

bool = @{ "#t" | "#f" }

COMMENT = _{
    ";" ~ (!NEWLINE ~ ANY)* ~ (NEWLINE | EOI)
  | "#|" ~ (!"|#" ~ ANY)* ~ "|#"
}

WHITESPACE = _{ " " | "\t" | NEWLINE }
